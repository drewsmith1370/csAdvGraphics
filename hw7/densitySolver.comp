#version 450

layout (local_size_x = 32, local_size_y = 32) in;

layout (std140, binding=0) uniform MouseUniformBuffer {
    vec2 mozCoord;
    float deltaTime;
    bool click;
} mozUbo;
// r, g, b channels are velocity vectors, a is density
layout (rgba32f, binding=0) uniform image2D FluidTex;

// const float grav = 0.98;
const float diffuse_rate = 100;
const float advect_rate  = 200;

vec4 getFluidInfo(ivec2 texel) {
    return imageLoad(FluidTex, texel);
}

ivec2 wrapEdges(ivec2 v) {
    v.x = int(mod(v.x, 512.0));
    v.y = int(mod(v.y, 512.0));
    return v;
}

vec4 diffusion(vec4 fluidInfo, ivec2 texel) {
    // Every frame swap densities with surrounding neighbors
    float dif = diffuse_rate * mozUbo.deltaTime;
    vec3 vel = fluidInfo.xyz;
    // Take neighboring texels
    ivec2 up   = wrapEdges(texel + ivec2(  0,  1));
    ivec2 down = wrapEdges(texel + ivec2(  0, -1));
    ivec2 left = wrapEdges(texel + ivec2( -1,  0));
    ivec2 rght = wrapEdges(texel + ivec2(  1,  0));

    float newDensity = fluidInfo.a + dif * (getFluidInfo(left).a + 
                                            getFluidInfo(down).a +
                                            getFluidInfo(  up).a +
                                            getFluidInfo(rght).a - 4*fluidInfo.a);

    return vec4(vel,newDensity);
}

// Solve advection backwards (what points density value will end up in this one?)
vec4 advection(vec4 fluidInfo, ivec2 texel) {
    // Use velocity to travel backwards
    vec2 advCoord = vec2(texel) - fluidInfo.xy * mozUbo.deltaTime * advect_rate;
    // find the surrounding coordinates
    vec2 intPart=vec2(0);
    vec2 interpHi = modf(advCoord, intPart);
    ivec2 bl = ivec2(intPart);
    ivec2 br = ivec2(bl.x+1,bl.y  );
    ivec2 ul = ivec2(bl.x  ,bl.y+1);
    ivec2 ur = ivec2(bl.x+1,bl.y+1);
    bl = wrapEdges(bl);
    br = wrapEdges(br);
    ul = wrapEdges(ul);
    ur = wrapEdges(ur);
    // Fractional part used for interpolating between the texel values
    // interpHi = fract(advCoord);
    vec2 interpLo = 1 - interpHi;
    // Sample the points surrounding the actual point and interpolate for new value
    float newVal = getFluidInfo(bl).a * interpLo.x*interpLo.y +
                   getFluidInfo(br).a * interpHi.x*interpLo.y +
                   getFluidInfo(ul).a * interpLo.x*interpHi.y +
                   getFluidInfo(ur).a * interpHi.x*interpHi.y;

    fluidInfo.a = newVal;
    return fluidInfo;
}

vec4 addSource(vec4 info, ivec2 texel) {
    vec2 dif = mozUbo.mozCoord - vec2(texel) / 512.0;
    float dst = dif.x*dif.x + dif.y*dif.y;
    if (dst < .00001) {
        info.xy = vec2(0);
        info.a = 1;
    }
    // Place a velocity function here for debugging purposes:
    // vec2 loc = vec2(cos(gl_GlobalInvocationID.y/5/3.14),sin(gl_GlobalInvocationID.x/5/3.14));
    // info.xy = normalize(loc);
    if (info.x < .1) info.x += .001;
    return info;
}

void main() {
    // Get invocation texel coord
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    memoryBarrier();

    vec4 fluidInfo = getFluidInfo(texelCoord);
    // Advection
    fluidInfo = advection(fluidInfo, texelCoord);
    // Diffusion
    fluidInfo = diffusion(fluidInfo, texelCoord);
    // Add source
    if (mozUbo.click) fluidInfo = addSource(fluidInfo, texelCoord);
    // Store new values
    barrier();
    imageStore(FluidTex, texelCoord, fluidInfo);
}
